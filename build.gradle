buildscript {
	repositories {
		mavenCentral()
	}
	
	dependencies {
		classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.6.0'
		classpath 'org.gradle.api.plugins:gradle-gae-plugin:0.8'
	}
}

allprojects {
	
	group = "com.bennavetta.appsite2"
	version = "0.0.1"
	
	ext {
		appengineVersion = "1.7.7.1"
		groovyVersion = "2.1.3"
		spockVersion = "0.7-groovy-2.0"
		guavaVersion = '14.0.1'
		objectifyVersion = '4.0b3'
	}
}

subprojects {
	apply plugin: 'eclipse'
	apply plugin: 'java'
	apply plugin: 'groovy'
	
	apply plugin: 'license'
	apply plugin: 'jacoco'
	apply plugin: 'checkstyle'
	apply plugin: 'findbugs'
	apply plugin: 'pmd'
	
	sourceCompatibility = 1.7
	targetCompatibility = 1.7
	
	repositories {
		mavenCentral()
		flatDir {
			dirs rootProject.file('lib')
		}
	}
	
	check.dependsOn jacocoTestReport
	jacocoTestReport {
		reports {
			html.enabled = true
			xml.enabled = true
		}
		additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
	}
	
	dependencies {
		compile "org.codehaus.groovy:groovy-all:$groovyVersion"
		compile "com.google.guava:guava:$guavaVersion"
		testCompile "org.spockframework:spock-core:$spockVersion"
		
		
	}
	
	license {
		header rootProject.file('config/HEADER')
		strictCheck true
		
		ext {
			year = Calendar.instance.get(Calendar.YEAR)
			name = 'Ben Navetta'
			email = 'ben.navetta@gmail.com'
		}
	}
	
	checkstyle {
		configFile = rootProject.file('config/checkstyle/checkstyle.xml')
	}
	
	tasks.withType(FindBugs) {
	    reports {
	        xml.enabled = false
	        html.enabled = true
	    }
	 }
	 
	 javadoc.excludes << "**/impl/**"
	 
	 task license {
		 dependsOn licenseFormatMain, licenseFormatTest
	 }
}

task wrapper(type: Wrapper) {
	gradleVersion = "1.6"
}